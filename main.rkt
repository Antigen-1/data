#lang racket/base
(require (for-syntax racket/base) racket/splicing)
(provide (all-defined-out))

(define-syntax (define-data stx)
  (syntax-case stx (lib representation abstraction)
      ((_ name
          (lib primitive ...)
          (representation (rp-name rp-body) ...)
          (abstraction (ab-name ab-body) ...))
       (let* ((make-id (lambda (str) (string->symbol (format "~a:~a" (syntax->datum #'name) str))))
              (rp-id (make-id "representation"))
              (ab-id (make-id "abstraction"))
              (add-rp (lambda (stx) ((make-interned-syntax-introducer rp-id) stx 'add)))
              (add-ab (lambda (stx) ((make-interned-syntax-introducer ab-id) stx 'add))))
         (with-syntax (((rp-name ...) (map add-rp (syntax->list #'(rp-name ...))))
                       ((rp-body ...) (map add-rp (syntax->list #'(rp-body ...))))
                       ((ab-name ...) (map add-ab (syntax->list #'(ab-name ...))))
                       ((ab-body ...) (map (compose add-ab add-rp) (syntax->list #'(ab-body ...)))))
           #`(begin
               (splicing-let () (local-require primitive ...) (define rp-name rp-body) ...)
               (splicing-let () (define ab-name ab-body) ...)
               (provide (for-space #f rp-name ... ab-name ...)
                        (for-space #,rp-id rp-name ...)
                        (for-space #,ab-id ab-name ...))
               ))))))
